plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'maven-publish'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

dependencies {
	implementation ('org.postgresql:postgresql:42.7.3')
	implementation('org.hibernate:hibernate-core:6.5.0.Final')
	implementation ('org.liquibase:liquibase-core:4.27.0')
	implementation('com.h2database:h2:2.2.224')
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

	implementation 'org.springframework.boot:spring-boot-starter-web:3.2.5'
	implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'

	implementation('org.springframework.boot:spring-boot-starter-data-jpa:3.2.5')

	implementation 'com.fasterxml.jackson.core:jackson-core:2.15.4'
	implementation('com.google.guava:guava:31.0.1-jre')

	implementation('org.springframework.boot:spring-boot-starter-validation:3.2.5')

	compileOnly('org.projectlombok:lombok:1.18.32')
	annotationProcessor('org.projectlombok:lombok:1.18.32')

	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.5'
	testImplementation('uk.org.webcompere:model-assert:1.0.0')

	testCompileOnly('org.projectlombok:lombok:1.18.32')
	testAnnotationProcessor('org.projectlombok:lombok:1.18.32')

	testImplementation('com.h2database:h2:2.2.224')
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform()
}

clean.doFirst {
	delete "$projectDir/logs", "$projectDir/out"
	logger.lifecycle("Deleted: logs, out")
}


jar {
	enabled = false
}

bootJar {
	enabled = true
	archiveBaseName = 'insurance-calculator'
	archiveVersion = '1.0.2'
	mainClass = 'org.example.travel.insurance.InsuranceApplication'
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			// Identify the artifact
			artifact bootJar  // This refers to the Spring Boot's bootJar task

			// Project coordinates
			groupId = 'com.example'
			artifactId = 'insurance-calculator'
			version = '1.0.0'

			// Add other metadata as needed
			pom {
				name = 'Insurance Calculator Application'
				description = 'Application to calculate travel insurance premium'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
			}
		}
	}

	// Configure the repository location (local Maven repository)
	repositories {
		mavenLocal()
	}
}

